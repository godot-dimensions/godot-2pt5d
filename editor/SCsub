#!/usr/bin/env python

Import("env")
Import("env_modules")

env_25d = env_modules.Clone()

env_25d.add_source_files(env.modules_sources, "*.cpp")
env_25d.add_source_files(env.modules_sources, "icons/*.cpp")
env_25d.add_source_files(env.modules_sources, "viewport/*.cpp")

# The rest of this file handles generating the 2.5D icons header file.
import icons.editor_2pt5d_icon_generator

env["BUILDERS"]["MakeEditor25DIconsBuilder"] = Builder(
    action=env.Run(icons.editor_2pt5d_icon_generator.make_editor_2pt5d_icons_action),
    suffix=".h",
    src_suffix=".svg",
)

target_file_path = "icons/editor_2pt5d_icons.gen.h"
icon_sources = Glob("../addons/2pt5d/icons/25D.svg") + Glob("../addons/2pt5d/icons/Node25D.svg")
env.Alias("editor_2pt5d_icons", [env.MakeEditor25DIconsBuilder(target_file_path, icon_sources)])
